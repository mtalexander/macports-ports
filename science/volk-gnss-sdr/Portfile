# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:filetype=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

PortGroup           cmake 1.0
PortGroup           github 1.0
PortGroup           active_variants 1.1

name                volk-gnss-sdr
maintainers         {michaelld @michaelld} {gmail.com:carles.fernandez @carlesfernandez} openmaintainer
description         Volk modules for GNSS-SDR
categories          science
license             GPL-3
platforms           darwin

dist_subdir         gnss-sdr

compiler.cxx_standard 2014

if {${subport} eq "volk-gnss-sdr"} {

    long_description    ${description}: \
        This port is kept up with the VOLK-GNSS-SDR release, which is typically updated every few months.

    github.setup        gnss-sdr gnss-sdr 0.0.14 v
    revision            0
    checksums           rmd160 71e9bbb7e125396be06d97b26f333e9e2bb4a9be \
                        sha256 48ba64073a995aa6f835c7a0cb775a741db9475359b036ca4245a65a6c1135fd \
                        size   4269976

    conflicts           volk-gnss-sdr-devel

}

subport volk-gnss-sdr-devel {
    long_description    ${description}: \
        This port is kept up with the VOLK-GNSS-SDR GIT next branch, which is typically updated daily to weekly. This version of VOLK-GNSS-SDR generally contains fixes and new features that will be incorporated in an upcoming release.

    name      volk-gnss-sdr-devel
    github.setup gnss-sdr gnss-sdr f75017e5206a2803f8dcd9dc6d1385bb939586c7
    version   20210111-[string range ${github.version} 0 7]
    checksums rmd160 6c5c60d95184c23a302c2f6867ec1ac619cd7cdc \
              sha256 29e715beb64877486ea9b09302ac3b4587dd85098a178ae4f98bb7d61fdbc95a \
              size   4271026
    revision  0

    conflicts volk-gnss-sdr

    # overload the github livecheck branch

    github.livecheck.branch next

}

# override github PortGroup homepage setting

homepage            https://gnss-sdr.org

depends_build-append port:pkgconfig

# can use either the Volk release or devel port.
# the Volk API is required, not the ABI.

depends_lib-append \
    path:lib/libvolk.dylib:volk \
    port:orc \
    port:boost

# specify the Python dependencies; these are checked for at configure,
# then used for building, but not at runtime.

set pythons_suffixes {27 36 37 38}

set pythons_ports {}
foreach s ${pythons_suffixes} {
    lappend pythons_ports python${s}
}

proc python_dir {} {
    global pythons_suffixes
    foreach s ${pythons_suffixes} {
        if {[variant_isset python${s}]} {
            set p python[string index ${s} 0].[string index ${s} 1]
            return [file normalize [exec ${p} -c "import sys; print(sys.prefix)"]/lib/${p}/site-packages]
        }
    }
    error "Python support not enabled."
}

foreach s ${pythons_suffixes} {
    set p python${s}
    set v [string index ${s} 0].[string index ${s} 1]
    set i [lsearch -exact ${pythons_ports} ${p}]
    set c [lreplace ${pythons_ports} ${i} ${i}]
    variant ${p} description "Build Volk using Python ${v}" conflicts {*}${c} "
    # required Python
    depends_lib-append \
        port:${p}
    depends_build-append \
        port:py${s}-six \
        port:py${s}-mako
    # specify the Python version to use
    configure.args-append \
        -DPYTHON_EXECUTABLE=${frameworks_dir}/Python.framework/Versions/${v}/bin/python${v} \
        -DVOLK_PYTHON_DIR=${frameworks_dir}/Python.framework/Versions/${v}/lib/python${v}/site-packages
    "
}

if {![variant_isset debug]} {
    set selected_python python37
    foreach s ${pythons_suffixes} {
        if {[variant_isset python${s}]} {
            set selected_python python${s}
        }
    }
    default_variants +${selected_python}
}

# do VPATH (out of source tree) build

cmake.out_of_source yes

# set work directory

worksrcdir ${worksrcdir}/src/algorithms/libs/volk_gnsssdr_module/volk_gnsssdr

# remove top-level library path, such that internal libraries are used
# instead of any already-installed ones.

configure.ldflags-delete -L${prefix}/lib

# specify where to install cmake files

configure.args-append \
    -DCMAKE_MODULES_DIR=${prefix}/share/cmake

test.run yes

# temporary fix to remove same-named files from 'volk' install
if {${subport} eq "volk-gnss-sdr"} {
    post-destroot {
        file delete ${destroot}${prefix}/bin/list_cpu_features
        file delete ${destroot}${prefix}/lib/libcpu_features.a
        file delete -force ${destroot}${prefix}/include/cpu_features
        file delete -force ${destroot}${prefix}/lib/cmake/CpuFeatures
    }
}
