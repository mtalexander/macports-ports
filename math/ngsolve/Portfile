# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               compilers       1.0
PortGroup               linear_algebra  1.0
PortGroup               github          1.0
PortGroup               cmake           1.1
PortGroup               active_variants 1.1

github.setup            NGSolve ngsolve 6.2.2004 v
revision                2
platforms               darwin
categories              math
license                 LGPL-2
maintainers             {mcalhoun @MarcusCalhoun-Lopez} openmaintainer
description             NGSolve Finite Element Library
long_description        NGSolve is a general purpose Finite Element Library \
                        on top of Netgen. With the basic library one can solve \
                        heat flow equations, Maxwell equations, and solid \
                        mechanical problems. Several add-ons are available for \
                        particular application classes.
homepage                https://ngsolve.org/

checksums               rmd160  282f5888ebc7be0379c60654a5df7a9b0a13aeb2 \
                        sha256  01b78d47b616a685cde7adca54ca7932bc7d278ebe6a06bc074f8977560f0505 \
                        size    15180583

compiler.cxx_standard   2017

compilers.choose        cc cxx
compilers.setup

# avoid conflict with atlas
patchfiles-append       patch-clapack.h.diff
post-extract {
    move    ${worksrcpath}/basiclinalg/clapack.h \
            ${worksrcpath}/basiclinalg/ng_clapack.h
}

# allow the use of other LAPACK libraries
patchfiles-append       patch-find-lapack.diff

depends_lib-append      port:SuiteSparse_AMD \
                        port:SuiteSparse_CCOLAMD \
                        port:SuiteSparse_CHOLMOD \
                        port:SuiteSparse_UMFPACK \
                        port:SuiteSparse_config \
                        path:include/turbojpeg.h:libjpeg-turbo \
                        port:netgen \
                        port:zlib

configure.args-append   -DNETGEN_DIR=${prefix} \
                        -DBUILD_UMFPACK=NO \
                        -DUSE_NATIVE_ARCH=NO

# avoid depends_lib-append port:bash
configure.args-append   -DBASH=/bin/bash

veclibfort              no
pre-configure {
    configure.args-append ${cmake_linalglib}
}

set pythonversions      {3.7 3.8 3.9}
set any_python          no
foreach pdv ${pythonversions} {
    set pv [join [lrange [split ${pdv} .] 0 1] ""]
    set conflist ""
    foreach v ${pythonversions} {
        if {${v} ne ${pdv}} {
            set vv [join [lrange [split ${v} .] 0 1] ""]
            set conflist "${conflist} python${vv}"
        }
    }
    variant python${pv} conflicts {*}${conflist} description "Add bindings for Python ${pdv}" {}
    if {[variant_isset python${pv}]} {
        set any_python  yes
    }
}
if {!${any_python}} {
    default_variants    +python39
}
set any_python          no
foreach pdv ${pythonversions} {
    set pv [join [lrange [split ${pdv} .] 0 1] ""]
    # settings that depend on loop variables must be set in an appropriate if, not in the
    # variant declaration scope.
    if {[variant_isset python${pv}]} {
        depends_lib-append      port:python${pv}
        depends_build-append    port:pybind11 \
                                port:py${pv}-pybind11-stubgen


        # during destroot, must find pybind11-stubgen binary, ngsolve Python module, and NGSolve library, so
        # must modify PATH, PYTHONPATH, and DYLD_LIBRARY_PATH
        #
        # however, DYLD environment variables are not inherited by subprocesses
        #
        # see https://lists.macports.org/pipermail/macports-dev/2021-April/043222.html
        post-extract {
            xinstall            -d -m 0755 ${cmake.build_dir}/macports_bin/bin
            xinstall            -m 0755 ${filespath}/pybind11-stubgen ${cmake.build_dir}/macports_bin/bin
        }
        post-patch {
            reinplace           "s|__MACPORTS_PYTHONPATH__|${destroot}${prefix}/lib/python${pdv}/site-packages/|g" \
                                ${cmake.build_dir}/macports_bin/bin/pybind11-stubgen
            reinplace           "s|__MACPORTS_DYLD_LIBRARY_PATH__|${destroot}${prefix}/lib|g" \
                                ${cmake.build_dir}/macports_bin/bin/pybind11-stubgen
            reinplace           "s|__MACPORTS_PYTHON_BIN__|${frameworks_dir}/Python.framework/Versions/${pdv}/bin|g" \
                                ${cmake.build_dir}/macports_bin/bin/pybind11-stubgen
        }
        build.env-append        PATH=${cmake.build_dir}/macports_bin/bin:$env(PATH)
        destroot.env-append     PATH=${cmake.build_dir}/macports_bin/bin:$env(PATH)

        set any_python          yes
        require_active_variants netgen python${pv}

        post-destroot {
            xinstall    -d -m 0755 \
                        ${destroot}${frameworks_dir}/Python.framework/Versions/${pdv}/lib/python${pdv}/site-packages
            ln -s       ${prefix}/lib/python${pdv}/site-packages/ngsolve \
                        ${destroot}${frameworks_dir}/Python.framework/Versions/${pdv}/lib/python${pdv}/site-packages/
        }
    }
}
if {!${any_python}} {
    foreach pdv ${pythonversions} {
        set pv [join [lrange [split ${pdv} .] 0 1] ""]
        require_active_variants netgen "" python${pv}
    }
}

post-destroot {
    delete ${destroot}${prefix}/Netgen.icns
}
